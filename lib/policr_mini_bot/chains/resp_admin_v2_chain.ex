defmodule PolicrMiniBot.RespAdminV2Chain do
  @moduledoc """
  `/admin_v2` å‘½ä»¤ã€‚
  """

  use PolicrMiniBot.Chain, {:command, :admin_v2}

  alias PolicrMiniWeb.AdminV2.TokenAuth
  alias PolicrMini.Accounts
  alias PolicrMini.Schema.User
  alias Telegex.Type.{InlineKeyboardButton, InlineKeyboardMarkup}

  require Logger

  def handle(%{chat: %{type: "private"}} = message, context) do
    %{chat: %{id: chat_id}, from: %{id: user_id} = from} = message

    owner_id = PolicrMiniBot.config_get(:owner_id)

    with {:owner, true} <- {:owner, user_id == owner_id},
         {:ok, token} <- create_token(from) do
      max_hours = div(TokenAuth.max_age(), 3600)

      text = """
      <b>ğŸ’» åå°ç®¡ç†</b>

      å·²ä¸ºæ‚¨åˆ›å»ºä¸€ä¸ªä¸´æ—¶ç™»å½•é“¾æ¥ï¼Œæœ‰æ•ˆæœŸ <code>#{max_hours}</code> å°æ—¶ã€‚ç‚¹å‡»ã€Œè¿›å…¥åå°ã€æŒ‰é’®å°†æ‰“å¼€æµè§ˆå™¨è®¿é—®åå°ï¼Œæ­¤é“¾æ¥åœ¨æœ‰æ•ˆæœŸå†…å¯å¤šæ¬¡è®¿é—®ã€‚

      <i>ğŸ¤« <b>è¯·å‹¿å°†æ­¤é“¾æ¥åˆ†äº«äºä»–äººï¼Œé™¤éæ‚¨æ­£åœ¨çŸ­æš‚çš„åˆ†äº«æ§åˆ¶æƒã€‚è‹¥æ€€ç–‘æ³„æ¼ï¼Œè¯·ç«‹å³åŠé”€</b></i>
      <i>âš ï¸ è¯·æ³¨æ„ï¼š<u>å½“å‰çš„åå°ä»…æ”¯æŒæ¡Œé¢æµè§ˆå™¨</u>ï¼Œè¯·å°½é‡ä½¿ç”¨ç”µè„‘è€Œä¸æ˜¯æ‰‹æœºæµè§ˆå™¨è®¿é—®ã€‚</i>
      """

      reply_markup = make_markup(token)

      case send_text(chat_id, text, reply_markup: reply_markup, parse_mode: "HTML") do
        {:ok, _} ->
          {:ok, context}

        {:error, reason} ->
          Logger.error("Command response failed: #{inspect(command: "/login", reason: reason)}")

          {:stop, context}
      end
    else
      {:error, :not_found} ->
        text = "æœªæ‰¾åˆ°å’Œæ‚¨ç›¸å…³çš„ç®¡ç†å‘˜è®°å½•ã€‚"

        send_text(chat_id, text, parse_mode: "HTML", logging: true)

        {:ok, context}
    end
  end

  def handle(message, context) do
    %{chat: %{id: chat_id}, message_id: message_id} = message
    text = commands_text("è¯·åœ¨ç§èŠä¸­ä½¿ç”¨æ­¤å‘½ä»¤ã€‚")

    case send_text(chat_id, text, reply_to_message_id: message_id) do
      {:ok, %{message_id: message_id}} ->
        async_delete_message_after(chat_id, message_id, 8)

      {:error, reason} ->
        Logger.error("Command response failed: #{inspect(command: "/login", reason: reason)}")
    end

    async_delete_message(chat_id, message_id)

    {:ok, %{context | deleted: true}}
  end

  @spec make_markup(String.t()) :: InlineKeyboardMarkup.t()
  defp make_markup(token) do
    root_url = PolicrMiniWeb.root_url(has_end_slash: false)

    %InlineKeyboardMarkup{
      inline_keyboard: [
        [
          %InlineKeyboardButton{
            text: commands_text("åŠé”€å…¨éƒ¨ä»¤ç‰Œ"),
            callback_data: "revoke:v2:admin"
          }
        ],
        [
          %InlineKeyboardButton{
            text: commands_text("è¿›å…¥åå°"),
            url: "#{root_url}/admin/v2?auth=#{token}"
          }
        ]
      ]
    }
  end

  @spec create_token(Telegex.Type.User.t()) :: {:ok, String.t()} | {:error, :not_found}
  defp create_token(from) do
    load_user =
      case Accounts.get_user(from.id) do
        nil ->
          sync_user(from)

        user ->
          {:ok, user}
      end

    case load_user do
      {:ok, user} ->
        token =
          Phoenix.Token.sign(PolicrMiniWeb.Endpoint, TokenAuth.namespace(), %{
            user_id: user.id,
            token_ver: user.token_ver
          })

        {:ok, token}

      err ->
        err
    end
  end

  @spec sync_user(Telegex.Type.User.t()) :: {:ok, User.t()} | {:error, any()}
  defp sync_user(from) do
    params = %{
      id: from.id,
      token_ver: 0,
      first_name: from.first_name,
      last_name: from.last_name,
      username: from.username
    }

    with {:ok, user} <- Accounts.upsert_user(from.id, params),
         {:ok, user} <- PolicrMiniBot.Helper.sync_user_photo(user) do
      {:ok, user}
    else
      err -> err
    end
  end
end
